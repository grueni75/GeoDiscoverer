apply plugin: 'com.android.application'

def computeVersionName() {
    def date = new Date()
    return date.format('yyyyMMdd')
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.2"
    ndkVersion "21.1.6352462"

    defaultConfig {
        applicationId "com.untouchableapps.android.geodiscoverer"
        minSdkVersion 23
        targetSdkVersion 23
        versionCode 28
        versionName computeVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "GeoDiscovererWear${variant.name.capitalize()}${variant.versionName}.apk";
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    splits {
        abi {
            enable gradle.startParameter.taskNames.any { it.contains("Debug") }
            reset()
            include 'armeabi-v7a'
            universalApk false
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.wear:wear:1.0.0'
    implementation 'com.google.android.support:wearable:2.2.0'
    compileOnly 'com.google.android.wearable:wearable:2.2.0'
    implementation 'com.google.android.gms:play-services-wearable:11.8.0'
    implementation project(':core')
}

task collectAssets(type: Exec) {
    workingDir 'src/main'
    commandLine '../../../collect_assets', 'wear'
}
preBuild.dependsOn collectAssets

android.buildTypes.all { buildType ->
    def setNativeCodeConfigTask = task("set${buildType.name.capitalize()}NativeCodeConfig", type: Exec) {
        commandLine 'echo', "${buildType.name}"
        doFirst {
            standardOutput = new FileOutputStream("core/src/main/mode.txt");
        }
    }
    def resetNativeCodeConfigTask = task("reset${buildType.name.capitalize()}NativeCodeConfig", type: Exec) {
        commandLine 'echo', "all"
        doFirst {
            standardOutput = new FileOutputStream("core/src/main/mode.txt");
        }
    }
    tasks.whenTaskAdded { task ->
        if (task.name == "check${buildType.name.capitalize()}Manifest") {
            task.dependsOn setNativeCodeConfigTask
        }
        if (task.name == "compile${buildType.name.capitalize()}Ndk") {
            task.dependsOn resetNativeCodeConfigTask
        }
    }
}

android.applicationVariants.all { variant ->
    if (variant.buildType.name == 'debug') {
        variant.outputs.each { output ->
            def taskSuffix = variant.name.capitalize()
            def assembleTaskName = "assemble${taskSuffix}"
            if (tasks.findByName(assembleTaskName)) {
                def copyAPKTask = tasks.create(name: "publish${taskSuffix}", type: Copy) {
                    from "${rootDir}/wear/build/outputs/apk/debug/${output.outputFile.name}"
                    into "${rootDir}/export/debug"
                    onlyIf {
                        if (inputs.sourceFiles.empty) {
                            ant.fail("Cannot find APK ${output.outputFile.name} to publish!")
                            return false;
                        } else
                            return true;
                    }
                }
                tasks[assembleTaskName].finalizedBy = [copyAPKTask]
            }
        }
    }
}
