apply plugin: 'com.android.application'


android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.untouchableapps.android.geodiscoverer"
        minSdkVersion 9
        targetSdkVersion 22
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            project.ext.set('appABI', 'all32')
        }
        debug {
            project.ext.set('appABI', 'armeabi-v7a')
            //project.ext.set('appABI', 'armeabi')
            //project.ext.set('appABI', 'x86')
        }
    }

    sourceSets.main {
        jniLibs.srcDir 'src/main/libs' // use the jni .so compiled from the manual ndk-build command
        jni.srcDirs = [] //disable automatic ndk-build call
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //wearApp project(':wear')
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:gridlayout-v7:22.2.0'
    compile project(':materialDialogs')
    compile project(':colorPickerPreference')
}

task collectAssets(type: Exec) {
    workingDir 'src/main'
    commandLine './collect_assets'
}
preBuild.dependsOn collectAssets

task updateVersion(type: Exec) {
    workingDir 'src/main'
    commandLine './update_version'
}
preBuild.dependsOn updateVersion

task ndkBuild(type: Exec) {
    workingDir 'src/main'
    commandLine 'ndk-build', 'APP_ABI=' + project.appABI
}
task collectSymbols(type: Exec) {
    workingDir 'src/main'
    commandLine './collect_symbols', 'obj'
}
preBuild.dependsOn collectSymbols
collectSymbols.dependsOn ndkBuild
