apply plugin: 'com.android.application'

def computeVersionName() {
    def date = new Date()
    return date.format('yyyyMMdd')
}

android {
    compileSdkVersion 21

    defaultConfig {
        applicationId "com.untouchableapps.android.geodashboard"
        minSdkVersion 21
        targetSdkVersion 21
        versionCode 4
        versionName computeVersionName()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "GeoDashboard${variant.name.capitalize()}${variant.versionName}.apk";
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

android.applicationVariants.all { variant ->
    if (variant.buildType.name == 'debug') {
        variant.outputs.each { output ->
            def taskSuffix = variant.name.capitalize()
            def assembleTaskName = "assemble${taskSuffix}"
            if (tasks.findByName(assembleTaskName)) {
                def copyAPKTask = tasks.create(name: "publish${taskSuffix}", type: Copy) {
                    from "${output.outputFile.parent}/${output.outputFile.name}"
                    into "${rootDir}/export/debug"
                    onlyIf {
                        if (inputs.sourceFiles.empty) {
                            ant.fail("Cannot find APK ${output.outputFile.name} to publish!")
                            return false;
                        } else
                            return true;
                    }
                }
                tasks[assembleTaskName].finalizedBy = [copyAPKTask]
            }
        }
    }
}
